apply plugin: 'scala'
apply plugin: 'application'

ext {
    scalaVersion = '2.11'
    scalaMinorVersion = '5'
}

mainClassName = 'sandbox.main.Main'

// Jar without external dependencies
jar {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
}

// Runnable jar with all dependencies
task fatJar(type: Jar) {
    manifest {
        attributes 'Main-Class': "$mainClassName"
    }
    baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

compileScala {
    scalaCompileOptions.additionalParameters = ['-feature', '-language:implicitConversions', '-Xlint']
}

repositories {
    jcenter()
}

ext {
    junitVersion = '4.12'
    scalacticVersion = '2.2.4'
    scalaCheckVersion = '1.12.1'
}

dependencies {
    compile "org.scala-lang:scala-library:$scalaVersion.$scalaMinorVersion"
    compile "org.scalactic:scalactic_$scalaVersion:$scalacticVersion"

    testCompile "junit:junit:$junitVersion"
    testCompile "org.scalatest:scalatest_$scalaVersion:$scalacticVersion"
    testCompile "org.scalacheck:scalacheck_$scalaVersion:$scalaCheckVersion"
}

test {
    useJUnit()

    testLogging {
        events "passed", "skipped", "failed"
    }

    // always run tests
    outputs.upToDateWhen { false }
}